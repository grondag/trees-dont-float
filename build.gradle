plugins {
	id 'fabric-loom' version '0.2.1-SNAPSHOT'
	id 'java'
	id 'maven'
	id 'maven-publish'
	id "com.zoltu.git-versioning" version "3.0.3"
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

def versionInfo = ZoltuGitVersioning.versionInfo
def autoVersion = "${versionInfo.major}.${versionInfo.minor}.${versionInfo.commitCount}"

archivesBaseName = "tdnf"
group = "grondag"
version = autoVersion

minecraft {
}

processResources {
	inputs.property "version", autoVersion
	from(sourceSets.main.resources.srcDirs) {
		include "fabric.mod.json"
		expand "version": version
	}
}

// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
// if it is present.
// If you remove this task, sources will not be generated.
task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = "sources"
	from sourceSets.main.allSource
}

sourceSets {
	main {
	        java {
	            include 'grondag/**/*'
	            exclude '*.DS_Store'
	        }
	        resources
	        {
	        	include '**/*'
	        	exclude '*.DS_Store'
	        }
	    }
    test {
        java {
            exclude 'grondag/**'
        }
    }
}

repositories {
    maven { url "https://tehnut.info/maven"}
    maven { name="BuildCraft"; url="https://mod-buildcraft.com/maven" }
    maven { url "https://minecraft.curseforge.com/api/maven" }
 	maven {
    	name = "grondag"
    	url = "https://grondag-repo.appspot.com"
    	credentials {
            username "guest"
            password ""
		}
    }
}

def mcVersion = "1.14"

dependencies {
	minecraft "com.mojang:minecraft:$mcVersion"
	mappings "net.fabricmc:yarn:$mcVersion+build.1"
	modCompile "net.fabricmc:fabric-loader:0.4.2+build.132"
	modCompile "net.fabricmc:fabric:0.2.7+build.127"
	
	compile "org.apiguardian:apiguardian-api:1.0.0"
	compile "com.google.code.findbugs:jsr305:3.0.2"

	modCompile "grondag:frex:0.4.+"
	modCompile "grondag:canvas:0.5.+"
	modCompile "io.github.prospector.modmenu:ModMenu:1.4.0-71"
	modCompile "roughly-enough-items:RoughlyEnoughItems:2.7.11.97"
	modCompile "cloth:ClothEvents:0.3.1.23"
    modCompile "cloth-config:ClothConfig:0.2.0.12"
    
	compile "blue.endless:jankson:1.1.0"
	compile "org.joml:joml:1.9.14"
}

publishing {
    publications {
        mavenJava(MavenPublication) {
			artifact(jar) {
				builtBy remapJar
			}
			artifact(sourcesJar) {
				builtBy remapSourcesJar
			}
        }
    }

    repositories {
        maven {
            url "https://grondag-repo.appspot.com"
            credentials {
	            username "${maven_user}"
	            password "${maven_password}"
			}
        }
    }
}